/*
 * Copyright (c) 2018-2019 AIO's author : Fred Montariol
 *
 * Use of this source code is governed by the GNU General Public License v2.0,
 * and is subject to the "Classpath" exception as provided in the LICENSE
 * file that accompanied this code.
 */

buildscript {
    ext {
        slf4j_version = '1.8.0-beta4'
        junit_version = '5.4.2'
        assertj_version = '3.12.2'
    }
    repositories {
        mavenCentral()
    }
}

println("Using Gradle version: $gradle.gradleVersion")

group 'org.aio'
version '0.1-SNAPSHOT'

subprojects {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation("org.slf4j:slf4j-api:$slf4j_version")
        
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.assertj:assertj-core:$assertj_version")
        
        testRuntimeOnly ("org.junit.jupiter:junit-jupiter-engine:$junit_version")
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
            showStandardStreams = true
        }
    }

    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--patch-module', "$moduleName=" + files(sourceSets.main.resources.srcDirs).asPath,
                ]
                classpath = files()
            }
        }
    }
}

// execute ./gradlew wrapper then remove .gradle directory when version change
wrapper {
    gradleVersion='5.4'
    distributionType=Wrapper.DistributionType.ALL
}

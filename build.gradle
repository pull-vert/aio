/*
 * Copyright (c) 2018-2019 AIO's author : Frédéric Montariol
 *
 * Use of this source code is governed by the GNU General Public License v2.0,
 * and is subject to the "Classpath" exception as provided in the LICENSE
 * file that accompanied this code.
 */

println("Using Gradle version: $gradle.gradleVersion")

group 'org.aio'
version '0.1-SNAPSHOT'

subprojects {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation('org.slf4j:slf4j-api:1.8.0-beta2')
        testImplementation('org.junit.jupiter:junit-jupiter-api:5.3.2')
        testImplementation('org.junit.jupiter:junit-jupiter-params:5.3.2')
        testImplementation('org.assertj:assertj-core:3.11.1')
        testRuntimeOnly ('org.junit.jupiter:junit-jupiter-engine:5.3.2')
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--patch-module', "$moduleName=" + files(sourceSets.main.resources.srcDirs).asPath,
                ]
                classpath = files()
            }
        }
    }
}

// execute ./gradlew wrapper then remove .gradle directory when version change
wrapper {
    gradleVersion='5.1'
    distributionType=Wrapper.DistributionType.ALL
}
